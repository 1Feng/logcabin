Import('env')

env.SharedObject("#build/test/gtest-all.o",
                 "#gtest/src/gtest-all.cc",
                 CPPPATH = [ "#gtest", "#gtest/include" ],
                 CPPFLAGS = [ "-g", "-DDEBUG" ],
                 CXXFLAGS = "-std=c++0x")
env.StaticLibrary("#build/test/gtest",
                 ["gtest-all.o"],
                 CPPPATH = [ "#gtest", "#gtest/include" ],
                 CPPFLAGS = [ "-g", "-DDEBUG" ],
                 CXXFLAGS = "-std=c++0x")

def SrcToVariant(srcs, variant_dir):
    """Find the corresponding paths to source files in a variant directory."""

    root = str(Dir('#'))
    variant_dir = str(Dir(variant_dir))
    return [str(src).replace(root, variant_dir) for src in srcs]

def GetTestFiles(src_dirs, variant_dir):
    """Find the test files to build in the given source directories.

    Given a list of source directories, return a list of strings naming the
    source copies to be placed in variant_dir of files ending in Test.cc that
    are directly contained in those directories.
    """
    return Flatten([SrcToVariant(Glob("%s/*Test.cc" % src_dir) +
                                 Glob("%s/*Mock.cc" % src_dir),
                                 variant_dir=variant_dir)
                    for src_dir in src_dirs])

env.Program("test",
            [
              "TestRunner.cc",
            ] +
            GetTestFiles([
                "#Core",
                "#Event",
                "#RPC",
                "#Protocol",
                "#Client",
                "#Storage",
                "#Server",
            ], variant_dir='#build'),
            LIBPATH = env["LIBPATH"] + [
                "#build/test",
                "#build/Server",
                "#build/Storage",
                "#build/Client",
                "#build/Protocol",
                "#build/RPC",
                "#build/Event",
                "#build/Core",
            ],
            LIBS = [
                     "libLogCabinServer",
                     "libLogCabinStorage",
                     "libLogCabinClient",
                     "libLogCabinProtocol",
                     "libLogCabinRPC",
                     "libLogCabinEvent",
                     "libLogCabinCore",
                     "gtest", "pthread", "protobuf", "rt", "cryptopp",
                     "event_core-2.0", "event_pthreads-2.0" ],
            CPPPATH = env["CPPPATH"] + ["#gtest/include"],
            # -fno-access-control allows tests to access private members
            CXXFLAGS = env["CXXFLAGS"] + ["-fno-access-control"])
